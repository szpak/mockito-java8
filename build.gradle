buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'info.solidsoft.gradle:cdeliveryboy:0.8.0'
        classpath('com.toomuchcoding:uptodate-gradle-plugin:1.0.1') {
            //Workaround: 0.7.2 is not available in Maven Central - https://github.com/marcingrzejszczak/uptodate-gradle-plugin/issues/2
            exclude group: 'org.codehaus.groovy.modules.http-builder', module: 'http-builder'
        }
    }
}

apply plugin: 'java'
apply plugin: 'com.toomuchcoding.uptodate'
apply from: "$rootDir/gradle/cdeliveryboy-release.gradle"
apply from: "$rootDir/gradle/manifest.gradle"

ext.mockitoVersion = findProperty('mockitoVersion') ?: '2.23.0'

sourceCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.compilerArgs << '-parameters'
}

test {
    useJUnitPlatform()

    afterSuite { desc, result ->
        if (!desc.parent) {
            //Workaround - https://github.com/gradle/gradle/issues/7452
            if (result.testCount == 0) {
                throw new IllegalStateException("No tests were found. Failing the build")
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.mockito:mockito-core:$mockitoVersion"
    compile 'net.jodah:typetools:0.6.1'

    testCompile 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
    testCompile "org.mockito:mockito-junit-jupiter:$mockitoVersion"
    testCompile 'org.assertj:assertj-core:3.12.2'
}

//Disable JavaDoc doclint on Java 8 - it fails on invalid JavaDoc elements in mockito-core jar
if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}
